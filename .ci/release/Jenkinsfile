#!/usr/bin/env groovy

@Library('apm@current') _
pipeline {
  agent { label 'linux && immutable' }
  environment {
    REPO = 'apm-agent-java'
    BASE_DIR = "src/github.com/elastic/${env.REPO}"
    NOTIFY_TO = credentials('notify-to')
    DOCKERHUB_SECRET = 'secret/apm-team/ci/elastic-observability-dockerhub'
    ELASTIC_DOCKER_SECRET = 'secret/apm-team/ci/docker-registry/prod'
    NEXUS_SECRET = 'secret/apm-team/ci/nexus'
    MAVEN_CONFIG = '-B -Dorg.slf4j.simpleLogger.log.org.apache.maven.cli.transfer.Slf4jMavenTransferListener=warn -Dmaven.repo.local=.m2'
    HOME = "${env.WORKSPACE}"
    JAVA_HOME = "${env.HUDSON_HOME}/.java/java10"
    PATH = "${env.JAVA_HOME}/bin:${env.PATH}"
  }
  options {
    timeout(time: 3, unit: 'HOURS')
    buildDiscarder(logRotator(numToKeepStr: '20', artifactNumToKeepStr: '20', daysToKeepStr: '30'))
    timestamps()
    ansiColor('xterm')
    durabilityHint('PERFORMANCE_OPTIMIZED')

  }
  parameters {
    string(name: 'branch_specifier', defaultValue: 'master')
  }

  stages {
    stage('Initializing'){
      options { skipDefaultCheckout() }
      stages {
        stage('Checkout') {
          steps {
            gitCheckout(
                basedir: "${BASE_DIR}",
                branch: 'master',
                repo: 'git@github.com:elastic/apm-agent-java.git',
                credentialsId: 'f6c7695a-671e-4f4f-a331-acdce44ff9ba',
                shallow: false
            )
            stash(allowEmpty: true, name: 'source', useDefaultExcludes: false)
            dir("${BASE_DIR}") {
              setupAPMGitEmail()
              script {
                env.TAG_BARE = sh(script: "git tag | tail -1", returnStdout: true)
                env.TAG_VER = sh(script: "git tag | tail -1 | sed s/v//", returnStdout: true)
                env.TAG_DOT_X = sh(script: 'git tag|tail -1|sed s/v//|cut -f1 -d "."|awk \'{print $1".x"}\'', returnStdout: true)
              }
            }
          }
        }
      }
    }
    stage('Release') {
      options { skipDefaultCheckout () }
      stages{
        stage('Check oss.sonatype.org') {
          steps {
            // If this fails, an exception should be thrown and execution will halt
            script {
              def r = sh(label: "Check Maven status", script: "scripts/jenkins/check_maven.sh -u https://status.maven.org/api/v2/summary.json --component OSSRH", returnStatus: true)
              if (r == 1) {
                error("Failing release build because Maven is the OSSRH component is not fully operational. See https://status.maven.org/ for more details.")
              }
            }
          }
        }
        stage('Check master build status') {
          steps {
            script {
              // If this build is not green: https://apm-ci.elastic.co/job/apm-agent-java/job/apm-agent-java-mbp/job/master/
              if(!buildStatus(host: 'apm-ci.elastic.co', job: ['apm-agent-java', 'apm-agent-java-mbp', 'master'], return_boolean: true)) {
                input(message: "WARNING! The master build is not passing. Do you wish to continue?")
              }
            }
          }
        }
        stage('Require confirmation that CHANGELOG.asciidoc has been updated') {
          steps {
            input(message: """
            Update CHANGELOG.asciidoc to reflect the new version release:
            Go over PRs or git log and add bug fixes and features.
            Move release notes from the Unreleased sub-heading to the correct [[release-notes-{major}.x]] sub-heading (Example PR for 1.13.0 release).

            Click 'Proceed' to confirm that this step has been completed or Abort to stop the build in order to complete this step.
            """
            )
          }
        }
        stage('Review project version') {
          steps {
            script {
              def ver = mvnVersion(showQualifiers: true)
              def should_continue = input(message: "Current version is ${ver}", parameters: [
                [
                  $class: 'ChoiceParameterDefinition',
                  name: "Do you wish to update the version?",
                  "choices": ["Yes", "No"],
                  description: "Selecting 'Yes' will allow you to select the new version in the next step."
                ]
              ])
              if (should_continue == 'Yes'){
                def new_version = input(message: "Please enter version to change to:", parameters:
                  [
                    [
                      $class: 'StringParameterDefinition',
                      defaultValue: "${ver}",
                      description: 'We will run mvn versions:set -DnewVersion=<NEW_VERSION>', name: 'New Version'
                    ]
                  ]
                )
                sh(name: "mavenVersionUpdate", script: "mvn versions:set -DnewVersion=${new_version}")
              } else {
                echo "Skipping version update"
              }
            }
          }
        }
        stage('Wait on internal CI') {
          steps {
            input(message: "Start the release job on the internal CI. Click 'Proceed' once the job has succeeded or click 'Abort' if the release has failed and then manually undo the release.")
          }
        }
        stage('Nexus release') {
          steps {
            script {
              def spid = getVault('nexus')["staging-profile-id"]
              dir("${BASE_DIR}"){
                withEnvMask(vars: [[var: "SPID", password: spid]]){
                  def foundStagingId = nexusFindStagingId(stagingProfileId: "${SPID}", groupId: "co.elastic.apm")
                  nexusCloseStagingRepository(stagingProfileId: "${SPID}", stagingId: foundStagingId)
                  nexusReleaseStagingRepository(stagingProfileId: "${SPID}", stagingId: foundStagingId)
                }
              }
            }
          }
        }
        stage('Branch creation') {
          steps {
            dir("${BASE_DIR}") {
              setupAPMGitEmail()
              withEnv(["BRANCH_NAME=tags/${env.TAG_BARE}"]){
                withGitRelease() {
                  script {
                    def curVer = mvnVersion()
                    sh(script: "./scripts/jenkins/branch_creation.sh ${curVer}")
                    def isMajor = sh(script: "./scripts/jenkins/is_major.sh ${curVer}", returnStatus: true)
                    if (isMajor == 0) {
                      input message: "This was a major version release. Please update the conf.yml in the docs repo before continuing", ok "Continue"
                    }
                  }
                  gitPush()
                }
              }
              
            }
          }
        }
        stage('Create GitHub release draft') {
          steps {
            deleteDir()
            unstash 'source'
            dir("${BASE_DIR}"){
              script {
                def curVer = mvnVersion(showQualifiers: false)
                // Construct the URL with anchor for the release notes
                // Ex: https://www.elastic.co/guide/en/apm/agent/java/current/release-notes-1.x.html#release-notes-1.13.0
                def finalUrl = sh(script: "scripts/jenkins/generate_release_notes_url.sh ${curVer}", , returnStdout: true)
                def ret = githubReleaseCreate(draft: true, name: "Release ${curVer}", body: "[Release Notes for ${curVer}](${finalUrl})")
                env.RELEASE_ID = ret['id']
                env.RELEASE_NOTES_URL = finalUrl
              }
            }

          }
        }
        stage('Wait for artifact to be available in Maven Central') {
          steps {
            script {
              def fullUrl = sh(script: "./scripts/jenkins/maven_artifact_url.sh ")
              echo "Sleeping for ten minutes to wait for Sonatype"
              sleep(time: 10, unit: "MINUTES")
              waitUntil(initialRecurrencePeriod: 60000) {
                script {
                  def ret = sh(script: "curl -fs $fullUrl >/dev/null 2>&1", returnStatus: true)
                  echo "Waiting for the artifacts to be published on Sonatype"
                  return ret == 0
                }
              }
            }
          }
        }
        stage('Update Cloudfoundry') {
          steps {
            deleteDir()
            unstash 'source'
            dir("${BASE_DIR}"){
              script {
                env.BRANCH_NAME = "master"
              }
              withGitRelease() {
                setupAPMGitEmail()
                sh(script: "./scripts/jenkins/update_cloudfoundry.sh")
                gitPush()
              }
            }
          }
        }
        stage('Publish release on GitHub') {
          steps {
            waitUntil(initialRecurrencePeriod: 60000) {
              script {
                echo "Waiting for the release notes to be available"
                def ret = sh(script: "scripts/jenkins/docs_check.sh $TAG_VER")
                return ret == 0
              }
            }
            githubReleasePublish(id: "${env.RELEASE_ID}")
          }
        }
        stage('Build and push Docker images') {
          steps {
            sh(label: "Build Docker image", script: "scripts/jenkins/build_docker.sh")
            // Get Docker registry credentials
            dockerLogin(secret: "${ELASTIC_DOCKER_SECRET}", registry: 'docker.elastic.co')
            sh(label: "Push Docker image", script: "scripts/jenkins/push_docker.sh")
          }
        }
      }
    }
  }
}
